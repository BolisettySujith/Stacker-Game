class StackerGame {
  field int level;
  field int score;
  field MovingThroughRow mover;
  field Stack stack;
  field boolean play;

  constructor StackerGame new() {
    let level = 0;
    let score = 0;
    let mover = MovingThroughRow.new();
    let stack = Stack.new();
    let play = true;
    return this;
  }

  method void run() {
    var char key;
    var int blocks;
    var String levelStr;
    let levelStr = String.new(2);
    do Drawer.GridMaker();
    do Output.moveCursor(21, 28);
    do Output.printString("S T A C K E R");
    
    //Game starts from here
    while (play) {
      do levelStr.setInt(level + 1);
      do Output.moveCursor(1, 1);
      do Output.printString("Level ");
      do Output.printString(levelStr);
      do Output.printString("/");
      do Output.printInt(Constants.LEVELS());
      let score = level*10;
      do Output.moveCursor(3, 1);
      do Output.printString("Score: ");
      do Output.printInt(score);
      //If SPACE KEY is not pressed the block will move right to left and left to right
      while (~(key = Constants.KEY_SPACE())) {
        let key = Keyboard.keyPressed();
        do mover.move();
        do Drawer.row(mover.getRow(), level);
      }
      //If SPACE KEY is pressed the block will fix at the posotion and go to the next level
      while (key = Constants.KEY_SPACE()) {
        let blocks = stack.add(mover.getRow(), level);
        do Drawer.row(stack.getRow(level), level);

        do gameState(blocks);
        let key = 0;
        do Sys.wait(1000);
      }
    }
    return;
  }

  method void gameState(int blocks) {
    //If block no blocks are present he will loos the game and display some message
    if (blocks = 0) {
      let play = false;
      do Sys.wait(1500);
      do Screen.clearScreen();
      do Output.moveCursor(10, 20);
      do Output.printString("!!! oops !!! YOU LOST !!!");
      do Output.moveCursor(12, 20);
      do Output.printString("# Better Luck Next Time #");
      do Output.moveCursor(14, 24);
      do Output.printString("YOUR SCORE :");
      do Output.printInt(score);
      do Sys.wait(2500);
    } else {
      //If he reached the last level then the game will complete then it will display a win message
      if (level = 14) {
        let play = false;
        do Sys.wait(1500);
        do Screen.clearScreen();
        do Output.moveCursor(10, 20);
        do Output.printString("$$ CONGRATULATION $$");
        do Output.moveCursor(12, 20);
        do Output.printString("## Hurry ## You win ##");
        do Output.moveCursor(14, 24);
        do Output.printString("YOUR SCORE :");
        do Output.printInt(score);
        do Sys.wait(2500);
      } else { 
        //The level will increment and he can go the next level 
        let play = true;
        let level = level + 1;
        do mover.setLevel(level, blocks);
      }
    }
    return;
  }

  method void dispose() {
    do mover.dispose();
    do stack.dispose();
    do Memory.deAlloc(this);
    return;
  }
}
